?start: ode

?ode: (parameters | states | expressions)*

?pair: NAME "=" SIGNED_NUMBER
?assignment : NAME "=" expression [ unit ]
?unit: "#" UNIT
parameters : "parameters" "(" [ COMPONENT_NAME "," ] pair ("," pair)*  ")"
states : "states" "(" [ COMPONENT_NAME "," ] [ INFO ","] pair ("," pair)* ")"
expressions: (assignment)+
    | "expressions" "(" COMPONENT_NAME [ "," INFO ] ")" (assignment)+
    | "component" "(" COMPONENT_NAME  [ "," INFO ] ")" (assignment)+

?expression: product
    | expression "+" product   -> add
    | expression "-" product   -> sub

?product: atom
    | product "*" atom  -> mul
    | product "/" atom  -> div

?atom: NUMBER   -> number
    | "-" atom         -> neg
    | NAME             -> name
    | "(" expression ")"


COMPONENT_NAME: ESCAPED_STRING
INFO: ESCAPED_STRING
UNIT: NAME

%ignore WS
%ignore WS_INLINE

%import common.CNAME -> NAME
%import common.NUMBER
%import common.SIGNED_NUMBER
%import common.WS
%import common.WS_INLINE
%import common.SH_COMMENT -> COMMENT
%import common.ESCAPED_STRING
